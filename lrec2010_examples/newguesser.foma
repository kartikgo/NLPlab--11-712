!read lexc defaultnom.lexc
!define guess;
!read lexc Adjpl.lexc
!define spnn;
!read lexc specialAdj.lexc
!define spadj;
!read lexc diminutive.lexc
!define dimm
define alphabet  [a | á | b | c | d | e | é |  f | g | h | i | í | j | k | l | m | n | ñ | Ñ | o | ó | p | q | r | s | t | u | ú | ü | Ü | v | w | x | y | z];

define Con [b | c | d | f | g | h | j | k | l | m | n | ñ | Ñ | p | q | r | s | t | v | w | x | y | z];

define word alphabet+;
define cin word [{ción}^1|o^1|a^1];
define guesN1 word o [{+Guess}:0];
define guesN2 word a [{+Guess}:0];
define guesN3 word {ción} [{+Guess}:0];
define guesN4 ~[cin] [{+Guess}:0];
read lexc Noungen.lexc
substitute defined guesN1 for "^GN1"
substitute defined guesN2 for "^GN2"
substitute defined guesN3 for "^GN3"
substitute defined guesN4 for "^GN4"
define Noun;

define nots word [{ísimo}^1|{ísima}^1|o^1|a^1];
define notsup word [{ísimo}^1|{ísima}^1];
define guesA1 [[word o] - notsup] [{+Guess}:0];
define guesA2 [[word a] - notsup] [{+Guess}:0];
!define guesA3 word {ísimo} [{+Guess}:0];
!define guesA4 word {ísima} [{+Guess}:0];
define guesA5 ~[nots] [{+Guess}:0];
read lexc Adjgen.lexc
substitute defined guesA1 for "^GA1"
substitute defined guesA2 for "^GA2"
!substitute defined guesA3 for "^GA3"
!substitute defined guesA4 for "^GA4"
substitute defined guesA5 for "^GA5"
define Adjective;

define guesV1 word {ar} [{+Guess}:0];
define guesV2 word {er} [{+Guess}:0];
define guesV3 word {ir} [{+Guess}:0];
read lexc verbgen.lexc
substitute defined guesV1 for "^GV1"
substitute defined guesV2 for "^GV2"
substitute defined guesV3 for "^GV3"
define Verb;

read lexc eieverb.lexc
define irVerb;

read lexc zir.lexc
define zVerb;

!Handling Superlatives
define sup1 o -> 0 || _ {^} {ísimo} ;
define sup2 a -> 0 || _ {^} {ísima} ;
!Handling Adverbs
define advb o -> a || _ {^} {mente} ;
!Handling pluralization
define conpl [..] -> e || b | c | d | f | g | h | j | k | l | m | n | ñ | Ñ | p | q | r | s | t | ü | Ü | v | w | x | y | í | ó | ú | á | é _ "^" s .#. ;

!define accpl í -> i e, ó -> o e, ú -> u e, á -> a e, é -> e e || _ "^" s .#.;

define cz z -> c e || _ "^" s ;

!Handle Verbs
define vbar {ar} -> 0 || _ {^} [o | a s | a | a m o s | á i s | a n | a b a | a b a s | á b a m o s | a b a i s | a b a n | é | a s t e | ó | a m o s | a s t e i s | a r o n | e | e s | e n | a r a | a r a s |  á r a m o s | a r a i s | a r a n | a s e | a s e s | á s e m o s | a s e i s | a s e n | a r e | a r e s | á r e m o s | a r e i s | a r e n | a n d o | a d o] ;

define vber {er} -> 0, {ir} -> 0 || _{^} [{iendo}|{en}|{ías}|{an}|{as}|{ieron}|{ían}|{iere}|{iéramos}|{iera}|{es}|{iste}|{iesen}|{íamos}|{isteis}|{imos}|{áis}|{ía}|{a}|{í}|{íais}|{ió}|{ierais}|{iéremos}|{iese}|{iereis}|{ieran}|{ieres}|{iésemos}|{e}|{ieseis}|{o}|{ido}|{amos}|{ieren}|{ieras}],,
{er} -> 0 || _{^}[{emos}|{éis}],,
{ir} -> 0 || _{^}{ís};

define red {ar} -> 0 || _{^^^} [o |{as}|a|{an}|e|{es}|{en}];
define rede {er} -> 0 || _{^^^} [o |{as}|a|{an}|e|{es}|{en}];
define redi {ir} -> 0 || _{^^^} [o |{as}|a|{an}|e|{es}|{en}];
define redii {ir} -> 0 || _{^^^^} [{amos} |{áis}|{ió}|{ieron}|{iera}|{ieras}|{iéramos}|{ierais}|{ieran}|{iese}|{ieses}|{iésemos}|{ieseis}|{iesen}|{iendo}];
define irr e -> {ie} || ?+ _ [? - e]+ {^^^};
define irr2 o -> {ue} || ?+ _ [? - o]+ {^^^};
define irr3 u -> {ue} || ?+ _ [? - u]+ {^^^};
define irr4 i -> {ie} || ?+ _ [? - i]+ {^^^};
define irr5 e -> i || ?+ _ [? - e]+ {^^^};
define irr6 e -> i || ?+ _ [? - e]+ {^^^^};
define irr7 c -> {zc} || _ {^};
define Cleanup1 {^^^} -> 0 ;
define Cleanup2 {^^^^} -> 0 ;

define Splclean {^^} -> 0 || [{ar}|{er}|{ir}] _ ;
define Cleanup {^} -> 0 ;

regex  [zVerb .o. vber .o. irr7 .o. Cleanup] .p. [irVerb .o. [red | rede | redi|redii] .o. [irr | irr2 | irr3|irr4|irr5|irr6] .o. [Cleanup1|Cleanup2]] .p. [([Noun|Adjective|Verb] .o. vbar .o. vber .o. conpl .o. cz .o. sup1 .o. sup2 .o. advb .o. Splclean .o.  Cleanup)] ;
